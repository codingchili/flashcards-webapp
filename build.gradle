import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'

project.version = "1.0.0-SNAPSHOT"
project.group = 'com.codingchili'

sourceCompatibility = 1.9
targetCompatibility = 1.9

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    compile 'com.github.codingchili.chili-core:core:1.0.5'
}

test {
    testLogging {
        exceptionFormat "full"
    }
    reports.html.enabled = false
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

ext {
    polymerBin = Os.isFamily(Os.FAMILY_WINDOWS) ? 'polymer.cmd' : 'polymer'
    bowerBin = Os.isFamily(Os.FAMILY_WINDOWS) ? 'bower.cmd' : 'bower'
}

task polymer {
    doLast {
        exec {
            workingDir "./polymer"
            commandLine = [bowerBin, "install"]
        }
        exec {
            workingDir "./polymer"
            commandLine = [polymerBin, "build"]
        }
    }
}


jar.dependsOn polymer

jar {
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes 'Implementation-Title': 'flashcards',
                'Implementation-Version': version,
                'Main-Class': 'com.codingchili.flashcards.Service'
    }
}

task alljavadoc(type: Javadoc) {
    source subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
}

task archiveJavadoc(type: Zip, dependsOn: alljavadoc) {
    baseName = 'javadocs'
    from fileTree(file("${buildDir}/docs/javadoc"))
}

task testReport(type: TestReport, dependsOn: 'build') {
    destinationDir = file("$buildDir/reports/allTests")
    reportOn subprojects*.test
}

task archiveTestReport(type: Zip, dependsOn: testReport) {
    baseName = 'testreport'
    from fileTree(file("$buildDir/reports/allTests"))
}
